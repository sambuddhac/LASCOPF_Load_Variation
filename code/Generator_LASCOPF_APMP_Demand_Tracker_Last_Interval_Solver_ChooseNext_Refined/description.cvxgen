# Produced by CVXGEN, 2019-02-05 18:20:03 -0500.
# CVXGEN is Copyright (C) 2006-2017 Jacob Mattingley, jem@cvxgen.com.
# The code in this file is Copyright (C) 2006-2017 Jacob Mattingley.
# CVXGEN, or solvers produced by CVXGEN, cannot be used for commercial
# applications without prior written permission from Jacob Mattingley.

# Filename: description.cvxgen.
# Description: A description of the CVXGEN problem.

dimensions
  dim=500
end

parameters
  rho positive # ADMM tuning parameter
  beta positive # APP tuning parameter for across the dispatch intervals
  betaInner positive # APP tuning parameter for across the dispatch intervals
  gamma positive # APP tuning parameter for across the dispatch intervals
  lambda_3; lambda_4 # APP Lagrange Multiplier corresponding to the complementary slackness
  gammaSC positive # APP tuning parameter
  lambda_1SC (dim) # APP Lagrange Multiplier corresponding to the complementary slackness
  RgMax positive; RgMin negative # Generator maximum ramp up and ramp down limits
  PgMax positive; PgMin nonnegative # Generator Limits
  c2 nonnegative; c1 nonnegative; c0 nonnegative # Generator cost coefficients, quadratic, liear and constant terms respectively
  Pg_N_init # Generator injection from last iteration for base case and contingencies
  Pg_N_avg # Net average power from last iteration for base case and contingencies
  Thetag_N_avg # Net average bus voltage angle from last iteration for base case and contingencies
  ug_N # Dual variable for net power balance for base case and contingencies
  vg_N #  Dual variable for net angle balance for base case and contingencies
  Vg_N_avg # Average of dual variable for net angle balance from last to last iteration for base case and contingencies
  PgPrevNu; PgNu; PgNuInner # Previous iterates of the corresponding decision variable values
  PgNext nonnegative
  A # Disagreement between the generator output values for the previous interval by the present and the previous interval, at the previous iteration
  B # Cumulative disagreement between the generator output values for the previous and next intervals by the present, next, and the previous intervals, at the previous iteration
  BSC (dim) # Cumulative disagreement between the generator output values for the previous and next intervals by the present, next, and the previous intervals, at the previous iteration
  selectZero # Selection parameter to include or not include the last interval for PgNext constraint on ramping select 0 to not include the constraint, and 1 otherwise
end

variables
  Pg # Generator real power output
  PgPrev # Generator's belief about its output in the previous interval
  Thetag # Generator bus angle for base case
end

minimize
  c2*square(Pg)+c1*Pg+c0+(beta/2)*(square(PgPrev-PgPrevNu)+square(Pg-PgNu))+(betaInner/2)*(square(Pg-PgNuInner))+(gammaSC)*(sum(Pg*BSC))+sum(Pg*lambda_1SC)+(gamma)*(PgPrev*A+Pg*B)-lambda_3*PgPrev-lambda_4*Pg+(rho/2)*(square(Pg-Pg_N_init+Pg_N_avg+ug_N)+square(Thetag-Vg_N_avg-Thetag_N_avg+vg_N))
subject to
  PgMin <= Pg <= PgMax
  RgMin <= selectZero*(PgNext-Pg) <= RgMax
  RgMin <= Pg-PgPrev <= RgMax
end
